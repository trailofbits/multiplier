#
# Copyright (c) 2021-present, Trail of Bits, Inc.
# All rights reserved.
#
# This source code is licensed in accordance with the terms specified in
# the LICENSE file found in the root directory of this source tree.
#

@PACKAGE_INIT@

set(MX_ENABLE_WEGGLI @MX_ENABLE_WEGGLI@)
set(MX_ENABLE_RE2 @MX_ENABLE_RE2@)
set(MX_ENABLE_VAST @MX_ENABLE_VAST@)

if(NOT TARGET @PROJECT_NAME@::mx-api)
  if(NOT TARGET Threads::Threads)
    find_package(Threads REQUIRED)
  endif()

  if(NOT TARGET CapnProto::cpnp-rpc)
    find_package(CapnProto CONFIG REQUIRED)
  endif()

  if(NOT TARGET zstd::libzstd_shared AND NOT TARGET zstd::libzstd_static)
    find_package(zstd CONFIG REQUIRED)
  endif()

  if(MX_ENABLE_RE2 AND NOT TARGET re2::re2)
    find_package(re2 CONFIG REQUIRED)
  endif()

  if(NOT TARGET glog::glog)
    find_package(glog REQUIRED)
  endif()

  if(NOT TARGET gap::gap)
    find_package(gap CONFIG REQUIRED)
  endif()
  
  if (MX_ENABLE_VAST AND NOT TARGET vast::vast)
    find_package(Z3 CONFIG REQUIRED)
    find_package(LLVM CONFIG REQUIRED)
    find_package(MLIR CONFIG REQUIRED)
    find_package(Clang CONFIG REQUIRED)
    find_package(VAST CONFIG REQUIRED)
  endif()

  if(MX_ENABLE_WEGGLI AND NOT TARGET weggli_native::static)
    find_package(weggli_native CONFIG REQUIRED)
  endif()

  include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
  check_required_components("@PROJECT_NAME@")
endif()
